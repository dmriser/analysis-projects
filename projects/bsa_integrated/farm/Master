#!/bin/tcsh 

# User Parameters 
set numberOfJobs = 40
set binDir       = "/u/home/dmriser/Analysis_v2/projects/asym2/src/"
set binName      = "fill"
set sourceDir    = "/u/home/dmriser/Analysis_v2/projects/asym2/farm/"
set outDir       = "/volatile/clas12/dmriser/farm_out/"
set outName      = "tonsOfAsymmetry_v10"
set runScript    = "goAnalyze"
set fileList     = "/u/home/dmriser/Analysis_v2/lists/good_files.dat"
set configDir    = "/u/home/dmriser/Analysis_v2/projects/asym2/config/farm_run/"


# Create the output
# dir if it doesnt exist
set configs = `getConfigs $configDir`
mkdir -p $outDir/$outName
cp $runScript $outDir/$outName/$runScript

foreach conf ($configs)
    mkdir -p $outDir/$outName/$conf
end

# Splits the master list into numberOfJobs
# smaller lists to be used on the nodes.
./buildFileList $numberOfJobs $fileList

# Get our list of jobs 
set allLists = `ls *.dat`

# Send the jobs
set currentList = 1
foreach list ($allLists) 
    ./buildJsub $allLists[$currentList] analysis.$currentList.jsub $binDir $binName $outDir/$outName $outName.$currentList.root $sourceDir $runScript $configDir
    mv $list $outDir/$outName/$list
    jsub analysis.$currentList.jsub 
    @ currentList++
end

# Clean 
rm analysis.*.jsub 
rm list.*.dat

# Brag about it 
echo " >> Done, output dir created. "
ls $outDir/$outName
